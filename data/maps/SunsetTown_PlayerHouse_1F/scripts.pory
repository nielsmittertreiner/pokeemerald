const LOCALID_MOM = 1

mapscripts SunsetTown_PlayerHouse_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_SUNSET_TOWN_STATE) > 0) {
            setobjectxyperm(LOCALID_MOM, 5, 6)
            setobjectmovementtype(LOCALID_MOM, MOVEMENT_TYPE_FACE_LEFT)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SUNSET_TOWN_STATE, 0 {
            lock
            turnobject(LOCALID_MOM, DIR_EAST)
            playse(SE_PIN)
            applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
            waitmovement(0)
            delay(32)
            msgbox("Come here, quickly!")
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, SunsetTown_PlayerHouse_1F_Movement_PlayerWalksToMom)
            waitmovement(0)
            load_speech_bubble(LOCALID_MOM)
            msgbox("I've heard DAD has something for you.\n"
                   "Perhaps you should go see him.\p"
                   "I have no idea what it is, thoughâ€¦")
            closemessage
            destroy_speech_bubble
            applymovement(OBJ_EVENT_ID_PLAYER, SunsetTown_PlayerHouse_1F_Movement_PlayerWalkBack)
            waitmovement(0)
            applymovement(LOCALID_MOM, SunsetTown_PlayerHouse_1F_Movement_MomWalksToChair)
            waitmovement(0)
            setvar(VAR_SUNSET_TOWN_STATE, 1)
            release
            end
        }
    ]
}

script SunsetTown_PlayerHouse_1F_EventScript_Mom {
    lock
    faceplayer
    load_speech_bubble(LOCALID_MOM)
    msgbox("Do you want to rest here\n"
           "for a while, dear?", MSGBOX_YESNO)
    destroy_speech_bubble
    closemessage
    switch (var(VAR_RESULT)) {
        case YES:
            fadescreen(FADE_TO_BLACK)
            playfanfare(MUS_HEAL)
	        waitfanfare
            special(HealPlayerParty)
            fadescreen(FADE_FROM_BLACK)
            load_speech_bubble(LOCALID_MOM)
            msgbox("Phew, that must be refreshing right?\p"
                   "Now have fun on your adventure\n"
                   "dear! Stay safe.")
            destroy_speech_bubble
            closemessage
            release
            end
        case NO:
            break
        case MULTI_B_PRESSED:
            break
    }
    if (var(VAR_SUNSET_TOWN_STATE) == 1)
    {
        msgbox("What would your dad have\n"
               "for you?", MSGBOX_NPC)
    }
    else
    {
        msgbox("Have fun on your adventure,\n"
               "dear! Stay safe out there.", MSGBOX_NPC)
    }
    end
}

movement SunsetTown_PlayerHouse_1F_Movement_PlayerWalksToMom {
    walk_down * 2
    walk_left * 4
}

movement SunsetTown_PlayerHouse_1F_Movement_PlayerWalkBack {
    lock_facing_direction
    walk_right
}

movement SunsetTown_PlayerHouse_1F_Movement_MomWalksToChair {
    walk_right
    walk_down
    face_left
}