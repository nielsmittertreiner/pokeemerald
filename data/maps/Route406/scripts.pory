const LOCALID_LEMONADE_HIKER = 1
const LOCALID_HIKER1 = 2
const LOCALID_HIKER2 = 3

mapscripts Route406_MapScripts {}

script Route406_EventScript_InitializeOureaCaveStoryline {
    lock
    delay(30)
    playse(SE_PIN)
    applymovement(LOCALID_HIKER1, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_HIKER1, Common_Movement_FaceDown)
    waitmovement(0)
    load_speech_bubble(LOCALID_HIKER1)
    msgbox("Earthquake! Get down!")
    destroy_speech_bubble
    closemessage
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 8)  // horizontal pan
	setvar(VAR_0x8006, 20) // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCameraSilent)
    waitstate
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 5)  // horizontal pan
	setvar(VAR_0x8006, 16) // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCameraSilent)
    waitstate
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 3)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCameraSilent)
    waitstate
    delay(30)
    applymovement(LOCALID_HIKER1, Route406_Movement_Hiker1LookAround)
    waitmovement(0)
    load_speech_bubble(LOCALID_HIKER1)
    msgbox("Phew, it seems the quake settled down…")
    destroy_speech_bubble
    closemessage
    playse(SE_PIN)
    applymovement(LOCALID_HIKER1, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(30)
    applymovement(LOCALID_HIKER1, Route406_Movement_Hiker1RunToPlayer)
    waitmovement(0)
    load_speech_bubble(LOCALID_HIKER1)
    msgbox("Wait… what's a kid like you\n"
           "doing here?!\p"
           "You could've gotten hurt!\p"
           "Come with me…")
    destroy_speech_bubble
    closemessage
    applymovement(LOCALID_HIKER1, Route406_Movement_Hiker1TakePlayerToCave)
    applymovement(OBJ_EVENT_ID_PLAYER, Route406_Movement_PlayerWalkToCave)
    waitmovement(0)
    setflag(FLAG_HIDE_ROUTE_406_CONSTRUCTION_WORKER)
    setvar(VAR_OUREA_CAVES_STATE, 1)
    setvar(VAR_OUREA_CAVES_LIGHTS_STATE, 1)
    warp(MAP_OUREA_CAVE_B2F, 5, 0, 0)
    waitstate
    release
    end
}

script Route406_EventScript_Hiker1 {
    if (var(VAR_OUREA_CAVES_STATE) > 0 && var(VAR_OUREA_CAVES_STATE < 4)) {
        msgbox("Phew, that earthquake was quite\n"
               "heavy!\p"
               "I can withstand quite some more though!\n"
               "Hahaha!", MSGBOX_NPC)
        end
    } else {
        msgbox("Us HIKERS belong in the mountains!\p"
               "On ROUTE 406 and the OUREA CAVES, I\n"
               "usually meet other comrades!", MSGBOX_NPC)
        end
    }
}

script Route406_EventScript_Hiker2 {
    lock
    face_down
    if (flag(FLAG_TEMP_1)) {
        applymovement(LOCALID_LEMONADE_HIKER, Common_Movement_FaceLeft)
        waitmovement(0)
        delay(60)
        applymovement(LOCALID_LEMONADE_HIKER, Common_Movement_FaceRight)
        waitmovement(0)
        load_speech_bubble(LOCALID_LEMONADE_HIKER)
        msgbox("I wonder what's down there?")
        destroy_speech_bubble
        closemessage
    } else {
        load_speech_bubble(LOCALID_LEMONADE_HIKER)
        msgbox("Hah, you thought I would fight\n"
           "you didn't you?\p"
           "Well… I could but I really don't\n"
           "feel like it now.\l"
           "Instead take a break, drink up\l"
           "and enjoy the view with me!")
        destroy_speech_bubble
        closemessage
        giveitem(ITEM_LEMONADE, 1)
        closemessage
    }
    release
    end
}

script Route406_EventScript_Terrance {
    trainerbattle_single(TRAINER_ROUTE406_TERRANCE, Route406_Text_TerranceIntro, Route406_Text_TerranceDefeat)
    msgbox(Route406_Text_TerrancePostBattle, MSGBOX_NPC)
    end
}

script Route406_EventScript_ItemSuperPotion {
    finditem(ITEM_SUPER_POTION)
    end
}

script Route406_EventScript_Sign {
    msgbox("{LEFT_ARROW} NAVIRE TOWN\n"
           "{UP_ARROW} OUREA CAVES", MSGBOX_SIGN)
    end
}

movement Route406_Movement_Hiker1LookAround {
    face_left
    delay_16
    delay_16
    face_right
    delay_16
    delay_16
    face_left
    delay_16
    delay_16
    face_right
    delay_16
}

movement Route406_Movement_Hiker1RunToPlayer {
    walk_fast_right * 3
    face_up
}

movement Route406_Movement_Hiker1TakePlayerToCave {
    walk_left * 6
    walk_up
    set_invisible
}

movement Route406_Movement_PlayerWalkToCave {
    walk_down
    walk_left * 6
    walk_up
}

text Route406_Text_TerranceIntro {
    "You know what's pretty metal?\n"
    "Rock climbing!"
}

text Route406_Text_TerranceDefeat {
    "You seem to be more prepared that I am…"
}

text Route406_Text_TerrancePostBattle {
    "Watch out for falling rocks in\n"
    "these parts."
}
